apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "id.gradle"
def localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())
def keystoreProperties = new Properties()
keystoreProperties.load(rootProject.file("keystore.properties").newDataInputStream())
android {
    signingConfigs {
        zhet {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion = '30.0.3'
    defaultConfig {
        applicationId localProperties["app_id"]
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 115
        versionName "2.15"
        buildConfigField "String[]", "IDS", "{$ids}"
        multiDexEnabled true
        buildConfigField "String", "SERVER", localProperties["server"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.zhet
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "MuzAll.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    buildFeatures {
        compose true
    }
    lintOptions {
        checkReleaseBuilds false
    }

    kotlinOptions {
        useIR = true
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.dagger:dagger-android-support:2.36'
    kapt 'com.google.dagger:dagger-compiler:2.36'
    kapt 'com.google.dagger:dagger-android-processor:2.36'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
    }
    //compose
    implementation 'androidx.activity:activity-compose:1.3.0-beta01'
    implementation 'androidx.compose.foundation:foundation:1.0.0-beta08'
    implementation 'androidx.compose.material:material:1.0.0-beta08'
    implementation 'androidx.compose.runtime:runtime:1.0.0-beta08'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0-alpha07'
    //ads
    implementation 'com.google.android.gms:play-services-ads:20.1.0'
}
apply plugin: 'com.google.gms.google-services'
task copyFile(type: Copy) {
    from(rootProject.file('crashlytics/google-services_muz_all.json')) {
        rename 'google-services_muz_all.json', 'google-services.json'
    }
    into(rootProject.file('app'))
}

gradle.projectsEvaluated {
    preBuild.dependsOn(copyFile)
}
