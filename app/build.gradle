apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "id.gradle"
def localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())
def keystoreProperties = new Properties()
keystoreProperties.load(rootProject.file("keystore.properties").newDataInputStream())
android {
    signingConfigs {
        zhet {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "muz.all"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 81
        versionName "1.80"
        buildConfigField "String[]", "IDS", "{$ids}"
        multiDexEnabled true
        buildConfigField "String", "SERVER", localProperties["server"]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.zhet
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "MuzAll.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.google.dagger:dagger-android-support:2.25.2'
    kapt 'com.google.dagger:dagger-compiler:2.25.2'
    kapt 'com.google.dagger:dagger-android-processor:2.25.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.gms:play-services-ads:18.2.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
//    implementation 'com.facebook.android:audience-network-sdk:5.3.0-beta2'
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.android.support:multidex:1.0.3'
}
apply plugin: 'com.google.gms.google-services'

task copyFile(type: Copy) {
    from(rootProject.file('crashlytics/google-services_muz_all.json')) {
        rename 'google-services_muz_all.json', 'google-services.json'
    }
    into(rootProject.file('app'))
}

gradle.projectsEvaluated {
    preBuild.dependsOn(copyFile)
}
